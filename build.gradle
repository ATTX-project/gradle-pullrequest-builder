buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: "groovy"
apply plugin: 'maven'
apply plugin: 'signing'

version = '1.0.RC1'
group = 'de.jodamob.gradle'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.github.kittinunf.fuel:fuel:1.4.0'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

pluginBundle {
    website = 'https://github.com/dpreussler/gradle-pullrequest-builder'
    vcsUrl = 'https://github.com/dpreussler/gradle-pullrequest-builder.git'
    description = 'Gradle plugin to commit, push local files xand create pull request on github'
    tags = ['git', 'github']

    plugins {
        PullRequestBuilderPlugin {
            id = 'de.jodamob.gradle.github'
            displayName = 'Gradle Github PullRequest builder'
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'gradle-pullrequest-builder'
                packaging 'jar'
                description 'Gradle plugin to commit, push local files xand create pull request on github'
                url 'https://github.com/dpreussler/gradle-pullrequest-builder'

                scm {
                    url 'https://github.com/dpreussler/gradle-pullrequest-builder.git'
                    connection 'scm:git@github.com:dpreussler/gradle-pullrequest-builder.git'
                    developerConnection 'scm:git@github.com:dpreussler/gradle-pullrequest-builder.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://github.com/dpreussler/gradle-pullrequest-builder/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dpreussler'
                        name 'Danny Preussler'
                    }
                }
            }
        }
    }
}